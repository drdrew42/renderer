################################################################################
# WeBWorK Online Homework Delivery System
# Copyright &copy; 2000-2022 The WeBWorK Project, https://github.com/openwebwork
#
# This program is free software; you can redistribute it and/or modify it under
# the terms of either: (a) the GNU General Public License as published by the
# Free Software Foundation; either version 2, or (at your option) any later
# version, or (b) the "Artistic License" which comes with this package.
#
# This program is distributed in the hope that it will be useful, but WITHOUT
# ANY WARRANTY; without even the implied warranty of MERCHANTABILITY or FITNESS
# FOR A PARTICULAR PURPOSE.  See either the GNU General Public License or the
# Artistic License for more details.
################################################################################

package WeBWorK::Utils;
use base qw(Exporter);

use strict;
use warnings;

use JSON;

our @EXPORT_OK = qw(
	wwRound
	getAssetURL
);

# usage wwRound($places,$float)
# return $float rounded up to number of decimal places given by $places
sub wwRound(@) {
	my $places = shift;
	my $float  = shift;
	my $factor = 10**$places;
	return int($float * $factor + 0.5) / $factor;
}

my $staticPGAssets;

# Get the url for static assets.
sub getAssetURL {
	my ($language, $file, $isThemeFile) = @_;

	# Load the static files list generated by `npm install` the first time this method is called.
	if (!$staticPGAssets) {
		my $staticAssetsList = "$ENV{PG_ROOT}/htdocs/static-assets.json";
		if (-r $staticAssetsList) {
			my $data = do {
				open(my $fh, "<:encoding(UTF-8)", $staticAssetsList)
					or die "FATAL: Unable to open '$staticAssetsList'!";
				local $/;
				<$fh>;
			};

			$staticPGAssets = JSON->new->decode($data);
		} else {
			warn "ERROR: '$staticAssetsList' not found!\n"
				. "You may need to run 'npm install' from '$ENV{PG_ROOT}/htdocs'.";
		}
	}

	# If a right-to-left language is enabled (Hebrew or Arabic) and this is a css file that is not a third party asset,
	# then determine the rtl varaint file name.  This will be looked for first in the asset lists.
	my $rtlfile = $file =~ s/\.css$/.rtl.css/r
		if ($language =~ /^(he|ar)/ && $file !~ /node_modules/ && $file =~ /\.css$/);

	# Now check to see if this is a file in the pg htdocs location with a rtl variant.
	# These also can only be local files.
	return "/pg_files/$staticPGAssets->{$rtlfile}" if defined $rtlfile && defined $staticPGAssets->{$rtlfile};

	# Next check to see if this is a file in the pg htdocs location.
	if (defined $staticPGAssets->{$file}) {
		# File served by cdn.
		return $staticPGAssets->{$file} if $staticPGAssets->{$file} =~ /^https?:\/\//;
		# File served locally.
		return "/pg_files/$staticPGAssets->{$file}";
	}

	# If the file was not found in the lists, then just use the given file and assume its path is relative to the pg
	# htdocs location.
	return "/pg_files/$file";
}

1;
